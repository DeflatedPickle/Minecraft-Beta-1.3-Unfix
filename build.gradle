plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'com.diffplug.spotless'
}

version = "$minecraft_version-$mod_version-$mod_loader"
group = "com.${developer.toLowerCase()}"
archivesBaseName = "$mod_id"

sourceCompatibility = targetCompatibility = "$jvm_version"

repositories {
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings:v$yarn_version"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

    modImplementation "net.fabricmc.fabric-api:fabric-api:$fabric_version"

    modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_kotlin_version+kotlin.$kotlin_version"
}

processResources {
    inputs.property "version", version

    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

tasks.withType(JavaCompile) {
    it.options.encoding = "UTF-8"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "$jvm_version"
    }
}

sourceSets.main.resources {
    srcDirs 'src/generated/resources/'
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()

        licenseHeader "/* $license_header */\n\n"
    }

    kotlin {
        ktlint()

        licenseHeader "/* $license_header */\n\n"
    }
}
